using GLib;
using Sqlite;

namespace NizamSettings {
    public enum PekwmApplyStatus {
        OK,
        RESTARTED,
        FAILED
    }

    public class PekwmBackend : Object {
                private static bool build_theme_in_place (File theme_dir, out string error_out) {
                    error_out = "";
                    if (!theme_dir.query_exists()) {
                        error_out = "theme directory not found";
                        return false;
                    }

                    if (!PekwmThemeBuilder.render_svg_to_png(theme_dir, out error_out)) {
                        return false;
                    }
                    if (!PekwmThemeBuilder.validate_theme(theme_dir, out error_out)) {
                        return false;
                    }
                    return true;
                }

                private static void copy_dir_recursive (File src, File dst) throws Error {
                    PekwmPaths.ensure_dir(dst);
                    var enumerator = src.enumerate_children("standard::name,standard::type", FileQueryInfoFlags.NONE);
                    FileInfo info;
                    while ((info = enumerator.next_file()) != null) {
                        var name = info.get_name();
                        var child_src = src.get_child(name);
                        var child_dst = dst.get_child(name);
                        if (info.get_file_type() == FileType.DIRECTORY) {
                            copy_dir_recursive(child_src, child_dst);
                        } else {
                            child_src.copy(child_dst, FileCopyFlags.OVERWRITE);
                        }
                    }
                }

        private static string xinitrc_contents () {
            
            return "#!/bin/sh\n" +
                   "# Generated by Nizam - DO NOT EDIT\n\n" +
                   "exec pekwm --config ~/.config/nizam/pekwm/config\n";
        }

        private static bool ensure_xinitrc (out string note) throws Error {
            note = "";
            var home = Environment.get_home_dir();
            var path = Path.build_filename(home, ".xinitrc");
            string existing = "";
            bool has_existing = false;
            try {
                if (FileUtils.test(path, FileTest.EXISTS)) {
                    has_existing = FileUtils.get_contents(path, out existing);
                }
            } catch (Error e) {
                
                has_existing = FileUtils.test(path, FileTest.EXISTS);
            }

            var desired = "exec pekwm --config ~/.config/nizam/pekwm/config";
            if (has_existing && existing.contains(desired)) {
                note = "~/.xinitrc already configured for pekwm (--config ~/.config/nizam/pekwm/config).";
                return false;
            }

            if (has_existing) {
                var ts = (new DateTime.now_local()).format("%Y%m%d-%H%M%S");
                var backup = path + ".bak-nizam-" + ts;
                var rc = FileUtils.rename(path, backup);
                if (rc == 0) {
                    note = "~/.xinitrc updated (backup: %s).".printf(backup);
                } else {
                    
                    note = "~/.xinitrc updated.";
                }
            } else {
                note = "~/.xinitrc created.";
            }

            FileUtils.set_contents(path, xinitrc_contents());
            FileUtils.chmod(path, 0755);
            return true;
        }

        private static WmState load_state (NizamDb db) throws Error {
            var st = new WmState();

            
            Statement stmt;
            var rc = db.handle.prepare_v2("SELECT key, value FROM wm_settings", -1, out stmt);
            if (rc == Sqlite.OK) {
                while ((rc = stmt.step()) == Sqlite.ROW) {
                    var key = stmt.column_text(0);
                    var value = stmt.column_text(1);
                    if (key != null && value != null) {
                        st.settings.insert(key, value);
                    }
                }
            }

            
            rc = db.handle.prepare_v2("SELECT accel, action, enabled FROM wm_keys ORDER BY id", -1, out stmt);
            if (rc == Sqlite.OK) {
                while ((rc = stmt.step()) == Sqlite.ROW) {
                    var accel = stmt.column_text(0) ?? "";
                    var action = stmt.column_text(1) ?? "";
                    var enabled = stmt.column_int(2) != 0;
                    st.keys.add(new WmKey(accel, action, enabled));
                }
            }

            return st;
        }

        public static PekwmApplyStatus apply_from_common (out string message, NizamDb? db = null) {
            message = "";
            try {
                
                var managed_dir = PekwmPaths.get_nizam_pekwm_dir();
                PekwmPaths.ensure_dir(managed_dir);

                
                
                var renderer = new PekwmRenderer();
                WmState st;
                if (db != null) {
                    st = load_state(db);
                } else {
                    st = new WmState();
                }
                PekwmPaths.atomic_write(PekwmPaths.get_nizam_pekwm_file("config"), renderer.render_config(st));

                
                var menu_dst = PekwmPaths.get_nizam_pekwm_file("menu");
                var menu_contents = PekwmMenuBuilder.build_hardcoded();
                PekwmPaths.atomic_write(menu_dst, menu_contents);

                
                var start_dst = PekwmPaths.get_nizam_pekwm_file("start");
                string start_contents;

                if (db != null) {
                    start_contents = PekwmStartBuilder.build_from_db(db);
                } else {
                    start_contents = PekwmStartBuilder.build(new PekwmStartConfig());
                }
                PekwmPaths.atomic_write(start_dst, start_contents);
                var start_path = start_dst.get_path();
                if (start_path != null && start_path.strip().length > 0) {
                    FileUtils.chmod(start_path, 0755);
                }

                
                var theme_src = PekwmPaths.find_theme_source_dir();
                if (theme_src == null || !theme_src.query_exists()) {
                    message = "Theme source not found";
                    return PekwmApplyStatus.FAILED;
                }
                var themes_dir = managed_dir.get_child("themes");
                PekwmPaths.ensure_dir(themes_dir);
                var nizam_theme_dst = themes_dir.get_child("nizam");
                copy_dir_recursive(theme_src, nizam_theme_dst);
                string theme_error;
                if (!build_theme_in_place(nizam_theme_dst, out theme_error)) {
                    message = "Theme build failed: %s".printf(theme_error);
                    return PekwmApplyStatus.FAILED;
                }

                
                string xinit_note;
                ensure_xinitrc(out xinit_note);

                
                message = "Setup complete. " + xinit_note + " Start X with startx (or relogin) to use the new config.";
                return PekwmApplyStatus.OK;
            } catch (Error e) {
                message = e.message;
                return PekwmApplyStatus.FAILED;
            }
        }

        public static PekwmApplyStatus apply_from_db (NizamDb db, out string message) {
            message = "";
            try {
                var st = load_state(db);
                var renderer = new PekwmRenderer();
                var managed_dir = PekwmPaths.get_nizam_pekwm_dir();
                PekwmPaths.ensure_dir(managed_dir);

                var config = renderer.render_config(st);
                var menu = PekwmMenuBuilder.build_hardcoded();
                var start = PekwmStartBuilder.build_from_db(db);

                PekwmPaths.atomic_write(PekwmPaths.get_nizam_pekwm_file("config"), config);
                PekwmPaths.atomic_write(PekwmPaths.get_nizam_pekwm_file("menu"), menu);
                var start_file = PekwmPaths.get_nizam_pekwm_file("start");
                PekwmPaths.atomic_write(start_file, start);
                FileUtils.chmod(start_file.get_path(), 0755);

                var theme_src = PekwmPaths.find_theme_source_dir();
                if (theme_src == null || !theme_src.query_exists()) {
                    message = "Theme source not found";
                    return PekwmApplyStatus.FAILED;
                }
                var themes_dir = managed_dir.get_child("themes");
                PekwmPaths.ensure_dir(themes_dir);
                var nizam_theme_dst = themes_dir.get_child("nizam");
                copy_dir_recursive(theme_src, nizam_theme_dst);
                string theme_error;
                if (!build_theme_in_place(nizam_theme_dst, out theme_error)) {
                    message = "Theme build failed: %s".printf(theme_error);
                    return PekwmApplyStatus.FAILED;
                }

                string xinit_note;
                ensure_xinitrc(out xinit_note);

                
                message = "Setup complete. " + xinit_note + " Start X with startx (or relogin) to use the new config.";
                return PekwmApplyStatus.OK;
            } catch (Error e) {
                message = e.message;
                return PekwmApplyStatus.FAILED;
            }
        }
    }
}
